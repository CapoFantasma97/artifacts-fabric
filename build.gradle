buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "artifacts.refmap.json"
}

version = '1.18.1-4.0.0'
group = 'artifacts'
archivesBaseName = 'artifacts'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.18.1'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            mods {
                artifacts {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            mods {
                artifacts {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'artifacts', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            environment 'target', 'fmluserdevdata'

            mods {
                artifacts {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main.resources.srcDirs += 'src/generated/resources'
}

repositories {
    maven {
        name "curios"
        url "https://maven.theillusivec4.top/"
    }
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        name "jei"
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name "expandability"
        url "https://maven.florens.be"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.1-39.0.8'

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    runtimeOnly fg.deobf("curse.maven:curious-armor-stands-398784:3579775")

    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18.1-5.0.4.1:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18.1-5.0.4.1")

    compileOnly fg.deobf("mezz.jei:jei-1.18.1:9.1.1.48:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.1:9.1.1.48")

    implementation fg.deobf("be.florens:expandability-forge:5.0.0")
}

mixin {
    add sourceSets.main, 'mixins.artifacts.refmap.json'
    config 'mixins.artifacts.json'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "artifacts",
            "Specification-Vendor": "ochotonida",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor": "ochotonida",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}